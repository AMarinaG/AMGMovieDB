apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: '../deployment/signing.gradle'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.amarinag.amgmoviedb"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/3/movie/\""
        buildConfigField "String", "IMAGE_URL", "\"https://image.tmdb.org/t/p/w500\""
        buildConfigField "String", "MOVIEDB_API", "\"$API_KEY\""
        vectorDrawables.useSupportLibrary = true
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.support.app_compat
    implementation deps.support.constraint
    implementation deps.support.design
    implementation deps.lifecycle.extensions
    implementation deps.timber
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.retrofit.rx2adapter
    implementation deps.stetho.core
    implementation deps.stetho.okhttp3
    implementation deps.okhttp3.okhttp
    implementation deps.okhttp3.logging_interceptor
    implementation deps.okhttp3.okhttp3_idling_resource
    implementation deps.support.espresso_idling_resource
    implementation deps.rxjava2
    implementation deps.rx_android
    implementation deps.glide.runtime
    implementation deps.glide.okhttp3_integration
    implementation deps.room.runtime
    implementation deps.room.rxjava2
    kapt deps.room.compiler

    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler
    kapt deps.databinding_compiler
    kapt deps.lifecycle.compiler
    kapt deps.glide.compiler

    //test dependencies
    testImplementation deps.junit
    testImplementation deps.room.testing
    androidTestImplementation deps.support.test_runner
    androidTestImplementation deps.support.espresso_core
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.flavorName ? variant.flavorName + "-" : ""}${variant.buildType.name}-${variant.versionName}.apk"
    }
}